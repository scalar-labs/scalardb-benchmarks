apply plugin:'application'
startScripts.enabled = false

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
    commonsCsvVersion = '1.9.0'
    commonsIoVersion = '2.11.0'
    guavaVersion = '30.1-jre'
    picoCliVersion = '4.1.4'
    slf4jVersion = '1.7.25'
    scalarDbVersion = '3.7.2'
}

dependencies {
    implementation group: 'com.google.guava', name: 'guava', version: "${guavaVersion}"
    implementation group: 'com.scalar-labs', name: 'scalardb', version: "${scalarDbVersion}"
    implementation group: 'commons-io', name: 'commons-io', version: "${commonsIoVersion}"
    implementation group: 'info.picocli', name: 'picocli', version: "${picoCliVersion}"
    implementation group: 'org.apache.commons', name: 'commons-csv', version: "${commonsCsvVersion}"
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: "${slf4jVersion}"
}

task TpccBench(type: CreateStartScripts) {
    mainClass = 'com.scalar.db.benchmarks.tpcc.TpccBench'
    applicationName = 'tpcc-bench'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtimeClasspath
}

task TpccLoader(type: CreateStartScripts) {
    mainClass = 'com.scalar.db.benchmarks.tpcc.TpccLoader'
    applicationName = 'tpcc-loader'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtimeClasspath
}

applicationDistribution.into('bin') {
    from(TpccBench)
    from(TpccLoader)
    fileMode = 0755
}
